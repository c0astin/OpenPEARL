MODULE(intergrationtest_mqtt_lm75_ads);
SYSTEM;
   Esp32MqttTcpClient('mqtt://192.168.178.20', 1883); !change ip or domain name to your mqtt_broker
!   Esp32MqttTcpClient('mqtt://berry-4', 1883);
   i2cbus_0: Esp32I2CBus(0, 21, 22, 100000);
   i2cbus_1: Esp32I2CBus(1, 18, 19, 400000);
   lm75_0: LM75('48'B4) --- i2cbus_0;
   lm75_1: LM75('4B'B4) --- i2cbus_0;
   ads: ADS1015SE('4A'B4, 0, 0) --- i2cbus_1;
   con: Console;
PROBLEM;

   SPC lm75_0 DATION IN SYSTEM BASIC FIXED(15) GLOBAL;
   SPC lm75_1 DATION IN SYSTEM BASIC FIXED(15) GLOBAL;
   SPC ads DATION IN SYSTEM BASIC FIXED(15) GLOBAL;
   SPC con DATION INOUT SYSTEM ALPHIC;
   
   DCL thermometer_0 DATION IN BASIC FIXED(15) CREATED(lm75_0);
   DCL thermometer_1 DATION IN BASIC FIXED(15) CREATED(lm75_1);
   DCL ads_0 DATION IN BASIC FIXED(15) CREATED(ads);
   DCL console DATION INOUT ALPHIC DIM(*,80) FORWARD STREAM CREATED(con);
   
   DCL consoleMutex SEMA PRESET(1);
   DCL temp0Mutex SEMA PRESET(1);
   DCL temp1Mutex SEMA PRESET(1);
   
   DCL global_temp_0 FLOAT;
   DCL global_temp_1 FLOAT;
   
mqttPublish: PROC(topic CHAR(40) , data CHAR(40) );
  DCL xx CHAR(40);
  xx = topic;

   __cpp__(
    "pearlrt::Esp32MqttTcpClient* cl = pearlrt::Esp32MqttTcpClient::getInstance();"
    "cl->publish(_topic, _data);"
);
END;

pub: TASK PRIO 1 MAIN;
    DCL topic CHAR(40) INIT('/topic/prl');
    DCL data CHAR(40);
    
    global_temp_0=0;
    global_temp_1=0;
    OPEN console;

    ACTIVATE readTemp_0;
    ACTIVATE readTemp_1;
    ACTIVATE readPoti;
    
    REPEAT
       REQUEST temp0Mutex;
       REQUEST temp1Mutex;
       CONVERT 'Temperatur_0=',global_temp_0, '| Temperatur_1=',global_temp_1 TO data BY A, F(5,1), A, F(5,1);
       RELEASE temp1Mutex;
       RELEASE temp0Mutex;
       
       REQUEST consoleMutex;
       PUT 'publish: topic=',topic,' data=',data TO console BY A,A,SKIP;
       RELEASE consoleMutex;
       
       mqttPublish(topic, data);
       AFTER 0.5 SEC RESUME;
    END;
END;

receiver: TASK PRIO 3;
   DCL topic CHAR(40) INIT('/topic/prl');
   DCL data CHAR(40);
     __cpp__(
          "pearlrt::Esp32MqttTcpClient* cl = pearlrt::Esp32MqttTcpClient::getInstance();"
         "cl->subscribe(_topic);"
   );
   REPEAT
     __cpp__(
          "cl->readMessage(_topic, _data);"
     );
       REQUEST consoleMutex;
       PUT 'received topic:',topic,'data: ',data TO console BY A,A,SKIP,A,A,SKIP;
       RELEASE consoleMutex;
   END;
END;

readTemp_0: TASK PRIO 1;
  DCL t FIXED(15);
  OPEN thermometer_0;
  REPEAT
     REQUEST temp0Mutex;
     TAKE t FROM thermometer_0;
     global_temp_0 = t/10;
     RELEASE temp0Mutex;
     AFTER 0.5 SEC RESUME;
  END;
  CLOSE thermometer_0;
END;

readTemp_1: TASK PRIO 1;
  DCL t FIXED(15);
  OPEN thermometer_1;
  REPEAT
     REQUEST temp1Mutex;
     TAKE t FROM thermometer_1;
     global_temp_1 = t/10;
     RELEASE temp1Mutex;
     AFTER 0.5 SEC RESUME;
  END;
  CLOSE thermometer_1;
END;

readPoti: TASK PRIO 2;
    DCL v0 FIXED(15);
    OPEN ads_0;
    REPEAT
        TAKE v0 FROM ads_0;
        REQUEST consoleMutex;
        PUT 'ADS115 values:', v0 TO console BY A, F(6), X(2), SKIP;
        RELEASE consoleMutex;
        AFTER 0.5 SEC RESUME;
    END;
    CLOSE ads_0;
END;
MODEND;
